// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Screen.jack

/**
 * A library of functions for displaying graphics on the screen.
 * The Hack physical screen consists of 512 rows (indexed 0..511, top to bottom)
 * of 256 pixels each (indexed 0..255, left to right). The top left pixel on 
 * the screen is indexed (0,0).
 */
class Screen {

    // each entry is a 16-pixel line, with 8192 = (24576 - 16384) entries
    static Array screen, twoToThe; 
    static boolean color, WHITE_COLOR, BLACK_COLOR;
    static int WHITE, BLACK, SCREEN_RANGE;

    /** Initializes the Screen. */
    function void init() {
        let screen = 16384;
        let SCREEN_RANGE = 8192;
        let WHITE_COLOR = false; // setting bit to 0 equals setting bit to be white
        let BLACK_COLOR = true; // setting bit to 1 equals setting bit to be black
        let WHITE = 0;
        let BLACK = -1;
        let color = BLACK;
        let twoToThe = Math.twoToTheMap();

        return;
    }

    /** Erases the entire screen. */
    function void clearScreen() {
        var int i;

        let i = 0;

        while (i < SCREEN_RANGE) {
            let screen[i] = WHITE;

            let i = i + 1;
        }

        return;
    }

    /** Sets the current color, to be used for all subsequent drawXXX commands.
     *  Black is represented by true, white by false. */
    function void setColor(boolean b) {
        let color = b;

        return;
    }

    /** 
     *  Draws the (x,y) pixel, using the current color. 
     *  The screen resolution is 512 (width) times 256 (height) pixels.
     *  0 <= x <= 511
     *  0 <= y <= 255
     */
    function void drawPixel(int x, int y) {
        var int addr, prevMap, whichBit, singleBitMap;

        // compute the word corresponding to this pixel
        // y represents the row number, and each row has 512 pixels or 32 words (32 * 16 = 512).
        // x represetns the pixel index on a same line; x / 16 is the word index on a line to which
        // this pixel belongs to.
        let addr = (y * 32) + (x / 16);

        // fetch current value/bitmap
        let prevMap = screen[addr];

        // compute the bit within this word corresponding to this pixel
        // and use it to get the corresponding map (default color is black)
        // let whichBit = x - (16 * (x / 16)); // mod
        let whichBit = x & 15; // bits more significant than 15 (2**4 - 1) are divisible by 16
        let singleBitMap = twoToThe[whichBit];

        // set value according to color
        if (color = BLACK_COLOR) {
            let screen[addr] = prevMap | singleBitMap;
        } else { // while the current color is WHITE
            // First, fliping the bits then AND the prevMap, we preserve all information in the prevMap
            // Second, the target bit is to be set as 0 or the color of white
            let screen[addr] = prevMap & ~singleBitMap;
        }

        return;
    }

    /** Draws a line from pixel (x1,y1) to pixel (x2,y2), using the current color. */
    function void drawLine(int x1, int y1, int x2, int y2) {
        var int dx, dy, ax, ay, diff; // diff of x, diff of y, actual curr x, actual curr y
        var int tempx, tempy;
        var boolean yInBound;

        // make x2 always >= x1 to simply scenarios
        if (~(x1 < x2)) {
            let tempx = x1;
            let tempy = y1;

            let x1 = x2;
            let y1 = y2;

            let x2 = tempx;
            let y2 = tempy;
        // this will overwrite the swapping above
        // } else { // x1 <= x2
        //     if (y1 > y2) {
        //         let tempx = x1;
        //         let tempy = y1;

        //         let x1 = x2;
        //         let y1 = y2;

        //         let x2 = tempx;
        //         let y2 = tempy;
        //     }
        }

        let dx = x2 - x1;
        let dy = y2 - y1;

        // Special case: horizontal or vertical?
        if (dx = 0) {
            if (y1 > y2) {
                do Screen.drawVertical(y2, y1, x1);
            } else {
                do Screen.drawVertical(y1, y2, x1);
            }
        } else { // x1 != x2
            if (dy = 0) { // vertical line
                // x1 != x2 but y1 == y2 -> horizontal line
                // make sure the inputs are in correct order
                if (x2 > x1) {
                    do Screen.drawHorizontal(x1, x2, y1);
                } else { // x2 <= x1
                    do Screen.drawHorizontal(x2, x1, y1);
                }
            }
        }

        // Generic case: if x1 != x2 && y1 != y2
        let ax = 0; 
        let ay = 0; // starting point (x1 + ax, y1 + ay), to the left of (x2, y2)
        let diff = 0;
        let yInBound = true;

        while (~(ax > dx) & yInBound) { // while ax <= dx and ay <= dy
            do Screen.drawPixel(x1 + ax, y1 + ay);
            
            if (dy > 0) {
                if (diff < 0) {
                    let ax = ax + 1;
                    let diff = diff + dy;
                } else {
                    let ay = ay + 1;
                    let diff = diff - dx;
                }
                let yInBound = ~(ay > dy);
            } else {
                if (diff < 0) {
                    let ay = ay - 1;
                    let diff = diff + dx;
                } else {
                    let ax = ax + 1;
                    let diff = diff + dy;
                }
                let yInBound = ~(ay < dy);
            }
        }
        
        return;
    }

    // function void drawPositiveSlopeLine(x1, y1, x2, y2) {
    //     
    // }
    // function void drawNegativeSlopeLine(x1, y1, x2, y2) {

    // }



    /**
     * Assume x2 > x1
     */
    function void drawHorizontal(int x1, int x2, int y) {
        var int startWordAddress, endWordAddress;
        var int i, j, xmod, edgeWord;

        if (x1 = x2) {
            do Screen.drawPixel(x1, y);
        }

        let startWordAddress = y * 32 + (x1 / 16);
        let endWordAddress = y * 32 + (x2 / 16);

        // if x1 and x2 belongs to the same word
        if (startWordAddress = endWordAddress) {
            do Screen.drawHorizontalWithinWord(x1, x2, y, startWordAddress);
            return;
        }
            
        // otherwise, this is a long line
        let j = startWordAddress + 1;

        // first word
        let edgeWord = 0;
        let xmod = x1 & 15;
        // made all bits more significant than x1 in this word drawn
        let i = 15;
        while (~(i < xmod)) {
            let edgeWord = edgeWord | twoToThe[i];
            let i = i - 1;
        }

        if (color = BLACK_COLOR) {
            let screen[startWordAddress] = screen[startWordAddress] | edgeWord;
        } else {
            let screen[startWordAddress] = screen[startWordAddress] & ~edgeWord;
        }

        // all words in between
        while (j < endWordAddress) {
            let screen[j] = color; // set all to be the current color
            // increment
            let j = j + 1;
        }

        // last word
        if (startWordAddress < endWordAddress) {
            let edgeWord = 0; // reuse local variable
            let xmod = x2 & 15;

            // made all bits less significant than x1 in this word drawn
            let i = 0;
            while (~(i > xmod)) {
                let edgeWord = edgeWord | twoToThe[i];
                let i = i + 1;
            }
            
            if (color = BLACK_COLOR) {
                let screen[endWordAddress] = screen[endWordAddress] | edgeWord;
            } else {
                let screen[endWordAddress] = screen[endWordAddress] & ~edgeWord;
            }
        }

        return ;
    }
    
    function void drawHorizontalWithinWord(int x1, int x2, int y, int wordAddr) {
        var int i, x1mod, x2mod, lineBitMap;

        let x1mod = x1 & 15; 
        let x2mod = x2 & 15; 
        let i = x1mod;
        let lineBitMap = 0;

        while (~(i > x2mod)) { // i <= x2
            let lineBitMap = twoToThe[i] | lineBitMap;

            let i = i + 1;
        }

        // write to the address, without overwriting previous bitmap
        if (color = BLACK_COLOR) {
            let screen[wordAddr] = screen[wordAddr] | lineBitMap;
        } else {
            let screen[wordAddr] = screen[wordAddr] & ~lineBitMap;
        }

        return;
    }
    
    /**
     * Set the same bit in words vertically aligned, or with address difference of 32.
     *  Assume y2 > y1
     */
    function void drawVertical(int y1, int y2, int x) {
        var int startWordAddress, endWordAddress;
        var int i, xmod, bitmapMask;

        if (y1 = y2) {
            do Screen.drawPixel(x, y1);
        }
        
        let startWordAddress = y1 * 32 + (x / 16);
        let endWordAddress = y2 * 32 + (x / 16);
        let xmod = x & 15;
        let bitmapMask = twoToThe[xmod];
        let i = startWordAddress;


        while (~(i > endWordAddress)) {
            if (color = BLACK_COLOR) {
                let screen[i] = screen[i] | bitmapMask;
            } else {
                let screen[i] = screen[i] & ~bitmapMask;
            }

            let i = i + 32;
        }
        
        return ;
    }

    /** 
     * Draws a filled rectangle whose top left corner is (x1, y1)
     * and bottom right corner is (x2,y2), using the current color. 
     * 
     * Draw (y2 - y1 + 2) horizontal lines. 
     * 
     */
    function void drawRectangle(int x1, int y1, int x2, int y2) {
        var int yi;

        let yi = y1;

        while (~(yi > y2)) {
            do Screen.drawHorizontal(x1, x2, yi);
            let yi = yi + 1;
        }
        
        return;
    }

    /** Draws a filled circle of radius r<=181 around (x,y), using the current color. */
    function void drawCircle(int x, int y, int r) {
        var int halfChord, dy, rSquared;

        let dy = 0;
        let rSquared = r * r;

        // step 1: while dy == 0        
        // save the unnecessary trouble of computing Math.sqrt(r * r) while dy == 0
        do Screen.drawHorizontal(x - r, x + r, y);

        // step 2
        // incrementing occurs at the block beginning, thus termianting condition 
        // should be dy < r rather than dy <= r
        while (dy < r) {
            // 0 < dy <= r
            // (y - r) <= (y - dy) <= y
            // y <= (y + dy) <= (y + r)
            let dy = dy + 1; 

            let halfChord = Math.sqrt(rSquared - (dy * dy));

            // upper half
            do Screen.drawHorizontal(x - halfChord, x + halfChord, y - dy);
            
            // lower half
            do Screen.drawHorizontal(x - halfChord, x + halfChord, y + dy);
        }

        // step 3: while dy == r
        // let halfChord = Math.sqrt(rSquared - (dy * dy));
        // do Screen.drawHorizontal(x - halfChord, x + halfChord, y - dy);
        // do Screen.drawHorizontal(x - halfChord, x + halfChord, y + dy);
        
        return;
    }
}
